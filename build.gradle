plugins {
    id 'application'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

project.group = "org.clyze"

applicationName = project.name
mainClassName = 'org.clyze.scanner.Main'

wrapper {
    gradleVersion = '5.4.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "${artifactory_contextUrl}/plast-public" }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

def groovyVersion='2.5.7'
dependencies {    
    compile 'org.clyze:clue-common:3.20.2'                     // Clue common
    compile 'org.clyze:metadata-model:1.0.1'
    compile "org.codehaus.groovy:groovy:${groovyVersion}"      // Groovy
    compile "org.codehaus.groovy:groovy-json:${groovyVersion}"
    compile "org.codehaus.groovy:groovy-cli-commons:${groovyVersion}"
    compile 'log4j:log4j:1.2.14'                               // Logging implementation
    compile 'commons-io:commons-io:2.4'                        // IO utils
}

jar {
    manifest {
        attributes 'Implementation-Version' : version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
        }
    }
}
if (project.hasProperty('artifactory_user')) {
publishing {
    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }
            //Always publish to the public releases repo
            url "${artifactory_contextUrl}/libs-public-release-local"
        }
    }
}
}

release {
    // Use defaults, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish
